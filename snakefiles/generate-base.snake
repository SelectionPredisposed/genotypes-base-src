# Config file and runtime variables
include: "variables.py"

# Catch all rule
rule all:
    input:
        expand(config['output_base'] + 'imputed/common/{chr}-dbsnp-unique-common.vcf.gz', chr=chromx)

# Merge imputed autosomal chromosomes
if config['dataset']=='HARVEST':
    rule merge_imputed:
        input:
            imp12=config['imputed_m12_path'] + "{chr}.vcf.gz",
            imp24=config['imputed_m24_path'] + "{chr}.vcf.gz"
        output:
            vcf=config['tmp_path'] + 'imputed/merged/{chr}-merged.vcf.gz',
            tbi=config['tmp_path'] + 'imputed/merged/{chr}-merged.vcf.gz.tbi'
        params:
            bcftools=bcftools
        shell:
            """
            {params.bcftools} merge --info-rules INFO:avg -o {output.vcf} -Oz {input.imp12} {input.imp24}
            tabix -f {output.vcf}
        """

# split the dbsnp file containing all chr into one file per chr to parallel subsequent operations
rule split_dbsnp:
    input:
        dbsnp=config['dbsnp'] 
    output:
        config['tmp_path'] + 'dbsnp/chr{chr}-dbsnp.vzf.gz' 
    shell:
        """
        bcftools view -r {wildcards.chr} {input.dbsnp} -Oz -o {output}
        tabix -f {output}
        """

# annotate markers without rsID from dbSNP reference 
rule annotate_missing_rsids:
    input:
        dbsnp=config['tmp_path'] + 'dbsnp/chr{chr}-dbsnp.vzf.gz', 
        vcf=config['tmp_path'] + 'imputed/merged/{chr}-merged.vcf.gz' if config['dataset']=="HARVEST" else config['imputed_path']
    output:
        vcf=config['tmp_path'] + 'imputed/dbsnp-update/{chr}-dbsnp.vcf.gz'
    shell:
        """
        bcftools annotate --annotations {input.dbsnp} --columns ID --output {output.vcf} --output-type z {input.vcf}
        """

# Convert rsids with dot-notation (unnamed) to format: chr<CHR>:<POSITION>_<REF>/<ALT>    
rule generate_unique_markers:
    input:
        config['tmp_path'] + 'imputed/dbsnp-update/{chr}-dbsnp.vcf.gz'
    output:
        config['output_base'] + 'imputed/all/{chr}-dbsnp-unique.vcf.gz'
    shell:
        """
        zcat {input} | awk -v OFS='\t' '$0~/^#/{{print; next}} {{snp[$3]++}} $3=="."{{$3="chr"$1":"$2"_"$4"/"$5}} snp[$3]>1 {{$3=$3"_"snp[$3]}} {{print}}' | gzip > {output}
        tabix -f {output}
        """

# Extract common markers
rule extract_common:
    input:
        config['output_base'] + 'imputed/all/{chr}-dbsnp-unique.vcf.gz'
    output:
        config['output_base'] + 'imputed/common/{chr}-dbsnp-unique-common.vcf.gz'
    params:
        vcftools=vcftools
    shell:
        """
        {params.vcftools} --gzvcf {input} --maf 0.001 --recode-INFO-all --recode --stdout | gzip -c > {output}
        tabix -f {output}
        """
